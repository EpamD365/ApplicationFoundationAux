<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MailSenderAux</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Mail sending auxiliary.
/// </summary>
public class MailSenderAux
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>enqueueMail</Name>
				<Source><![CDATA[
    /// <summary>
    /// Puts the mail into the mail queue.
    /// </summary>
    /// <param name = "_toEmailAddr">to email address.</param>
    /// <param name = "_emailTemplate">email template.</param>
    /// <param name = "_emailMessage">email message.</param>
    /// <param name = "_emailParameters">email parameters.</param>
    /// <param name = "_subject">mail subject.</param>
    /// <param name = "_filename">filename.</param>
    /// <param name = "_fileBinData">attachment.</param>
    /// <param name = "_emailStatus">email default status.</param>
    /// <returns>email item id.</returns>
    public SysEmailItemId enqueueMail(SysEmailRecipients    _toEmailAddr,
                               SysEmailTable         _emailTemplate,
                               SysEmailMessageTable  _emailMessage,
                               SysEmailParameters    _emailParameters,
                               Str                   _subject,
                               Filename              _filename,
                               container             _fileBinData,
                               SysEmailStatus        _emailStatus = SysEmailStatus::Unsent)
    
    {
        SysOutgoingEmailTable   outgoingEmailTable;
        SysOutgoingEmailData    outgoingEmailData;
        SysEmailItemId          nextEmailItemId;
        SysEmailContents        messageBody;
        Counter                 counter;
        Filename                filePath;
        Filename                filename;
        Filename                fileExtension;
        ;
    
        counter         = 1;
        nextEmailItemId = EventInbox::nextEventId();
        messageBody     = _emailMessage.Mail;
        ttsBegin;

        SysEmailParameters emailParameters = SysEmailParameters::find(); 
    
        outgoingEmailTable.EmailItemId      = nextEmailItemId;
        outgoingEmailTable.TemplateId       = _emailTemplate.EmailId;
        outgoingEmailTable.IsSystemEmail    = NoYes::No;
        outgoingEmailTable.Sender           = emailParameters.SMTPUserName;
        outgoingEmailTable.SenderName       = _emailTemplate.SenderName;
        outgoingEmailTable.Recipient        = _toEmailAddr;
        outgoingEmailTable.Origin           = _emailTemplate.Description;
        outgoingEmailTable.Message          = messageBody;
    
        outgoingEmailData.clear();
        outgoingEmailData.initValue();

        outgoingEmailData.EmailItemId       = nextEmailItemId;
        outgoingEmailData.DataId            = counter;
        outgoingEmailData.EmailDataType     = SysEmailDataType::Embedded;
        outgoingEmailData.Data              = _fileBinData;
        [filePath, filename, fileExtension] = Global::fileNameSplit(_filename);
            
        outgoingEmailData.FileName      = int642str(nextEmailItemId) + '_' + int2str(counter);
        outgoingEmailData.FileExtension = fileExtension;
            
        outgoingEmailData.insert();
               
        outgoingEmailTable.Subject      = _subject;
    
        outgoingEmailTable.Priority     = _emailTemplate.Priority;
        outgoingEmailTable.WithRetries  = true;
        outgoingEmailTable.RetryNum     = 0;
        outgoingEmailTable.UserId       = curuserid();
        outgoingEmailTable.Status       = _emailStatus;
    
        outgoingEmailTable.LatestStatusChangeDateTime = DateTimeUtil::getSystemDateTime();
        outgoingEmailTable.insert();
    
        ttsCommit;

        return outgoingEmailTable.EmailItemId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sendMail</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sends mail.
    /// </summary>
    /// <param name = "_toEmailAddr">to email address.</param>
    /// <param name = "_filePath">filepath.</param>
    /// <param name = "_subject">subject.</param>
    /// <param name = "_body">body.</param>
    /// <param name = "_binData">attachment.</param>
    /// <param name = "_emailId">email template id.</param>
    /// <param name = "_languageId">language id.</param>
    /// <param name = "_emailStatus">email default status.</param>
    /// <returns>email item id.</returns>
    public SysEmailItemId sendMail(SysEmailAddress    _toEmailAddr,
                         str                _filePath,
                         SysEmailSubject    _subject,
                         SysEmailContents   _body,
                         container          _binData,
                         SysEmailId         _emailId = '',
                         LanguageId         _languageId = 'en-US',
                         SysEmailStatus     _emailStatus = SysEmailStatus::Unsent)
    {
        SysEmailParameters          emailParameters;
        SysEmailMessageTable        emailMessage;
        SysEmailTable               emailTemplate;
        SysEmailItemId              emailItemId;
        ;
    
        emailParameters = SysEmailParameters::find();
    
        emailMessage = SysEmailMessageTable::find(_emailId, _languageId);
    
        if (_body)
        {
            emailMessage.Mail = _body;
        }
    
        emailItemId = this.enqueueMail(_toEmailAddr, emailTemplate, emailMessage, emailParameters, _subject, _filePath, _binData, _emailStatus);
    
        if (emailItemId != 0)
        {
            info(strFmt("@ApplicationFoundationAux:IMailWillBeSent", _toEmailAddr, "@SYS94142")); // The current report will be sent to %1 via %2 as attached PDF file
        }
        else
        {
            throw error("@ApplicationFoundationAux:EMailSendinFailed"); // There is a problem with report sending. Please contact your system administrator
        }

        return emailItemId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>attachFile</Name>
				<Source><![CDATA[
    /// <summary>
    /// Attaches file.
    /// </summary>
    /// <param name = "_nextEmailItemId">email item id.</param>
    /// <param name = "_outgoingEmailData">mail data.</param>
    /// <param name = "_path">path.</param>
    /// <param name = "_emailParameters">email parameters.</param>
    /// <param name = "_counter">counter.</param>
    /// <returns>true if success.</returns>
    private boolean attachFile(SysEmailItemId              _nextEmailItemId,
                               SysOutgoingEmailData        _outgoingEmailData,
                               Filename                    _path,
                               SysEmailParameters          _emailParameters,
                               Counter                     _counter)
    
    {
        Filename                filePath;
        Filename                fileName;
        Filename                fileExtension;
        
        if (this.fileSize(_path) < (_emailParameters.MaxEmailAttachmentSize * 1000000))
        {
            [filePath, filename, fileExtension] = Global::fileNameSplit(_path);
            _outgoingEmailData.EmailItemId       = _nextEmailItemId;
            _outgoingEmailData.DataId            = _counter;
            _outgoingEmailData.EmailDataType     = SysEmailDataType::Attachment;
            _outgoingEmailData.Data              = this.loadFileData(_path);
            _outgoingEmailData.FileName          = fileName;
            _outgoingEmailData.FileExtension     = fileExtension;
            _outgoingEmailData.insert();
            
            return true;
        }
        else
        {
            return false;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fileExists</Name>
				<Source><![CDATA[
    /// <summary>
    /// checks if file exists.
    /// </summary>
    /// <param name = "_filename">filename.</param>
    /// <returns>true if exists.</returns>
    protected boolean fileExists(Filename _filename)
    {
        return System.IO.File::Exists(_filename);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fileSize</Name>
				<Source><![CDATA[
    /// <summary>
    /// gets size of file.
    /// </summary>
    /// <param name = "_filename">filename.</param>
    /// <returns>size as int.</returns>
    protected int fileSize(Filename _filename)
    {
        System.IO.FileInfo fi = new System.IO.FileInfo(_fileName);

        return System.Convert::ToInt32(fi.Length);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEmailLanguageId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets email language id.
    /// </summary>
    /// <param name = "_emailTemplate">template.</param>
    /// <returns>language id.</returns>
    private LanguageId getEmailLanguageId(SysEmailTable _emailTemplate)
    {
        LanguageId ret;
        
        ret = _emailTemplate.DefaultLanguage;
        
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>loadFileData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Loads binary data from file.
    /// </summary>
    /// <param name = "_path">filepath.</param>
    /// <returns>binary data as container.</returns>
    protected container loadFileData(Filename _path)
    {
        BinData                 binData;
        container               embeddedBinaryData;
        
        binData = new BinData();
        binData.loadFile(_path);
        embeddedBinaryData = binData.getData();
        binData.finalize();
        binData = null;
        
        return embeddedBinaryData;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>